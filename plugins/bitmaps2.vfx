: mblit  ( m bmp - )
    ?dup 0= if drop exit then  at@f swap al_draw_bitmap ;

: blit  ( bmp - )
    0 swap mblit ;

: bmpw  ( bmp - n )  al_get_bitmap_width ;
: bmph  ( bmp - n )  al_get_bitmap_height ;
: bmpwh  ( bmp - w h )  dup bmpw swap bmph ; 
: loadbmp  ( zstr - )  al_load_bitmap ;    

: tilexy  ( n bw tw th - x y )
    locals| th tw bw n |
    n bw tw / mod tw *
    n bw tw / / th * ;

: subbmp  ( bmp w h - bmp )
    2>r at@ 2r@ al_create_sub_bitmap 2r> drop 0 +at ;

: tileset  ( bmp tw th - )
    locals| th tw bmp |
    0 0 at
    bmp bmph th / 0 do
        at@
        bmp bmpw tw / 0 do
            bmp tw th subbmp ,
        loop
        th + at
    loop ;
    
: tile  ( n a - )
    over 0< if 2drop exit then
    swap dup 28 rshift -rot $0fffffff and cells + @ mblit ;
