0 value display
0 value mixer
0 value voice
create mi /ALLEGRO_MONITOR_INFO allot&erase
0 value queue
0 value builtin-font  \ builtin-font
create m  16 cells allot  \ default matrix
  m al_identity_transform
create kbs0 /ALLEGRO_KEYBOARD_STATE allot&erase
create kbs1 /ALLEGRO_KEYBOARD_STATE allot&erase
create ms0 /ALLEGRO_MOUSE_STATE allot&erase
create ms1 /ALLEGRO_MOUSE_STATE allot&erase

: -audio
    mixer 0= if exit then
    mixer 0 al_set_mixer_playing drop ;

: +audio
    mixer if  mixer 1 al_set_mixer_playing drop  exit then 
    44100 ALLEGRO_AUDIO_DEPTH_INT16 ALLEGRO_CHANNEL_CONF_2 al_create_voice to voice
    44100 ALLEGRO_AUDIO_DEPTH_FLOAT32 ALLEGRO_CHANNEL_CONF_2 al_create_mixer to mixer
    mixer voice al_attach_mixer_to_voice 0= abort" Couldn't initialize audio"
    mixer al_set_default_mixer drop
    mixer 1 al_set_mixer_playing drop ;

: check  0= abort" Allegro init error" ;

: reset-keyboard
    kbs0 /ALLEGRO_KEYBOARD_STATE erase
    kbs1 /ALLEGRO_KEYBOARD_STATE erase
    al_uninstall_keyboard  al_install_keyboard drop
    queue al_get_keyboard_event_source al_register_event_source ;

: init-allegro
    ALLEGRO_VERSION 0 al_install_system check
    
    al_init_image_addon check
    al_init_native_dialog_addon check
    al_init_primitives_addon check
    al_init_font_addon
    al_init_ttf_addon check
    al_init_acodec_addon check
    al_install_audio 0= abort" Error installing audio."
    al_install_haptic check
    al_install_joystick check
    al_install_mouse check
    al_install_touch_input check
    ALLEGRO_VSYNC  1  ALLEGRO_SUGGEST  al_set_new_display_option
    [ doublebuf? dev? or ] [if]
        \ gets us one less frame of input lag
        \ and in development keeps the framebuffer always presented
        ALLEGRO_SINGLE_BUFFER 1 2 al_set_new_display_option
    [then]
    
    0 al_set_new_display_adapter
    [ fullscreen? ] [if]
        ALLEGRO_FULLSCREEN_WINDOW al_set_new_display_flags
        winw winh al_create_display to display
    [else]
        winw winh al_create_display to display
        display 0 0 al_set_window_position
    [then]
    
    0 to mixer  0 to voice
    64 al_reserve_samples 0= abort" Allegro: Error reserving samples." 
    +audio
    
    al_create_event_queue to queue
    queue  display       al_get_display_event_source  al_register_event_source
    queue                al_get_mouse_event_source    al_register_event_source
    al_create_builtin_font to builtin-font

    ALLEGRO_ADD ALLEGRO_ALPHA ALLEGRO_INVERSE_ALPHA al_set_blender
    
    reset-keyboard ;

: poll-keyboard
    kbs0 kbs1 /ALLEGRO_KEYBOARD_STATE move
    kbs0 al_get_keyboard_state ;

: poll-mouse
    ms0 ms1 /ALLEGRO_MOUSE_STATE move
    ms0 al_get_mouse_state ;

mswin? [if]
    extern void * GetForegroundWindow( );
    extern bool SetForegroundWindow( void * hwnd );
    
    GetForegroundWindow constant vfx-hwnd
[then]

init-allegro

mswin? dev? and [if]
    \ vfx-hwnd SetForegroundWindow drop
[then]
